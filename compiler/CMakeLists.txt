#
# CMake build rules for MERCATOR front end
#
# MERCATOR
# Copyright (C) 2018 Washington University in St. Louis; all rights reserved.

project(MERCATOR_FRONTEND
        LANGUAGES CUDA CXX)

cmake_minimum_required(VERSION 3.13.4)

find_package(LLVM REQUIRED CONFIG
  PATHS ${LLVM_PATH} NO_DEFAULT_PATH)

find_package(Clang REQUIRED CONFIG
  PATHS ${LLVM_PATH} NO_DEFAULT_PATH)

find_package(FLEX)

find_package(BISON)
			
#######################################################################
# File dependency list
#######################################################################

add_executable(mercator
	main.cc
	options.cc
	parse.cc
	parser-driver.cc
	typecheck.cc
	app.cc
	buildapp.cc
	topoverify.cc
	codegen/Formatter.cc
	codegen/gen_hostapp_class.cc
	codegen/gen_deviceapp_class.cc
	codegen/gen_deviceapp_ctor.cc
	${CMAKE_CURRENT_BINARY_DIR}/mercator-lexer.cc
	${CMAKE_CURRENT_BINARY_DIR}/mercator-parser.tab.cc
)


#######################################################################
# Compilation flags
#######################################################################

set_property(TARGET mercator PROPERTY CXX_STANDARD 14)

set (CMAKE_CXX_FLAGS_DEBUG   
     "${CMAKE_CXX_FLAGS_DEBUG} -O0 -UNDEBUG -Wall -DVERBOSE")

set (CMAKE_CXX_FLAGS_RELEASE 
     "${CMAKE_CXX_FLAGS_RELEASE} -O3 -DNDEBUG")

set (CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -Wall")

# avoid warnings about type punning from LLVM Clang headers
set_source_files_properties(typecheck.cc
       PROPERTIES COMPILE_FLAGS
       "${LLVM_CXXFLAGS} -fno-strict-aliasing"
)

######################################################################
# Include paths needed for compilation
######################################################################

target_include_directories(mercator
        PRIVATE ${CMAKE_CURRENT_BINARY_DIR}
        PRIVATE ${CMAKE_CURRENT_BINARY_DIR}/../runtime
	PRIVATE ${LLVM_INCLUDE_DIRS}
)

######################################################################
# Build magic for MERCATOR's parser and lexer
######################################################################

FLEX_TARGET(MercatorLexer mercator-lexer.ll 
			  ${CMAKE_CURRENT_BINARY_DIR}/mercator-lexer.cc)


BISON_TARGET(MercatorParser mercator-parser.yy 
			    ${CMAKE_CURRENT_BINARY_DIR}/mercator-parser.tab.cc
             DEFINES_FILE   ${CMAKE_CURRENT_BINARY_DIR}/mercator-parser.tab.hh)

ADD_FLEX_BISON_DEPENDENCY(MercatorLexer MercatorParser)

# we must add source dir to include path so that generated
# mercator-parser.tab.hh can include files from the source dir.
include_directories(${CMAKE_CURRENT_SOURCE_DIR})

######################################################################
# Generated configuration header
######################################################################

# get the LLVM version, which is needed by the configuration header
execute_process(COMMAND ${LLVM_PATH}/bin/llvm-config --version
                OUTPUT_VARIABLE LLVM_VERSION
                OUTPUT_STRIP_TRAILING_WHITESPACE)

execute_process(COMMAND ${LLVM_PATH}/bin/llvm-config --libdir
                OUTPUT_VARIABLE LLVM_LIBDIR
                OUTPUT_STRIP_TRAILING_WHITESPACE)

configure_file(build_config.h.in
               build_config.h)

######################################################################
# Supplement to cleanup rules
######################################################################

# CMake doesn't know about these extra files generated by various tools.
# Make sure we clean them up when we say "make clean"
set(EXTRA_FILES
    ${CMAKE_CURRENT_BINARY_DIR}/location.hh
    ${CMAKE_CURRENT_BINARY_DIR}/position.hh
    ${CMAKE_CURRENT_BINARY_DIR}/stack.hh
)

set_directory_properties(PROPERTIES
  ADDITIONAL_MAKE_CLEAN_FILES "${EXTRA_FILES}"
)

######################################################################
# Build library dependencies for mercator
# If not using a DYLIB build of LLVM, use llvm-config to get a list
# of libraries to link instead of the single LLVM dep below.
######################################################################

target_link_libraries(mercator
  clang-cpp
  LLVM
)

###############################################################

install(TARGETS mercator
	RUNTIME DESTINATION bin)
